plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.10.4'
    id "jacoco"
    id "org.sonarqube" version "7.0.1.6134"
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

group 'com.LuisPPB16'
version '1.1.1'

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2024.2'
        bundledPlugin 'com.intellij.java'
        instrumentationTools()
    }

    implementation 'net.datafaker:datafaker:2.0.2'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'com.h2database:h2:2.2.224'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "com.LuisPPB16_db-seed-plugin"
        property "sonar.organization", "luisppb16-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('test', Test) {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn tasks.named('test')
    reports {
        xml.getRequired().set(true)
        html.getRequired().set(true)
    }
}

tasks.named('buildSearchableOptions') {
    enabled = false
}
